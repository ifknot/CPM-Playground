;
.8080
NAME ('STDIO')
TITLE C-like macros for the I/O subroutines

; Writes a character to the console device
; Parameters:
;	ch - optional ASCII byte character to be written
; Return value in A register:
; 	On success, returns the written character.
;	On failure, returns EOF
putchar		MACRO 	ch
			IFNB <ch>			; if ch is not blank
				PUSH 	PSW
				MVI		A, ch	; ch->A
			ENDIF				
			CALL	PCHR##
			IFNB	<ch>
				POP		PSW
			ENDIF
			ENDM

; Get a character from the console device into register A	
; Return value in A register:
;	ASCII value character in keyboard buffer		
getchar		MACRO 	
			CALL	GCHR##
			ENDM

; Write carriage return and linefeed to console device
crlf		MACRO
			putchar		CR##
			putchar		LF##
			ENDM
			
; Prints the hex value of its optional BYTE argument num or A
printhex	MACRO	num
			IFNB	<num>	
				PUSH 	HL
				MVI 	A, num
			ENDIF
			CALL	PHEX##
			IFNB	<num>
				POP		HL
			ENDIF
			ENDM

; Prints the hex value of its optional WORD argument num or HL
printdhex	MACRO	num
			IFNB	<num>
				PUSH 	HL
				LXI		HL, num
			ENDIF
			CALL	PDHEX##
			IFNB	<num>
				POP		HL
			ENDIF
			ENDM			
			
; Prints the binary value of its optional BYTE argument num or A
printbin	MACRO	num
			IFNB	<num>	
				PUSH	PSW
				MVI 	A, num
			ENDIF
			CALL	PBIN##
			IFNB	<num>	
				POP 	PSW
			ENDIF
			ENDM

; Prints the binary value of its optional WORD argument num or HL	
printdbin	MACRO	num
			IFNB	<num>
				PUSH 	HL
				LXI		HL, num
			ENDIF
			CALL	PDBIN##
			IFNB	<num>
				POP		HL
			ENDIF
			ENDM		

; Prints the zero terminated string at <label> builds the data <label: string,0>
printzstr	MACRO 	label,string
			PUSH	HL
			LXI		HL, label
			CALL	PZSTR##
			POP 	HL
DSEG	; let the linker relocate the zero terminated string
label:		string, 0
CSEG	; restore everything following to relocatable code segment	
			ENDM
			
; Sets up a user stack of specified size	
; Parameters:
;	size - the numbers of bytes to reserve for the user stack space
; Return value:
; 	The SP register now points to the top of the user stack
setstack 	MACRO size
			DSEG					; data segment relocatable code
			DS		size			; uninitialized stack space	
TOPSTACK:	DB		0				; top of the user stack
			CSEG					; code segment relocatable code
			LXI		SP, TOPSTACK	; set up the user's stack 
			ENDM
			
; Exits back to the command-line processor (CCP) 
; Parameters:
;	err - a BYTE error code or 0 on success
; Return value:
; 	The err value is converted to ASCI and sent to CON:
exit		MACRO 	err
			printhex	err
			CALL 	RESET##
			ENDM
